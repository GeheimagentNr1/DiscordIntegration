plugins {
    id 'eclipse'
    id 'maven-publish'
    id 'net.minecraftforge.gradle' version '5.1.+'
    id("com.jaredsburrows.license") version "0.9.0"
    id "net.linguica.maven-settings" version "0.5"
    id "com.github.johnrengelman.shadow" version "7.1.2"
}

delete "src/main/java/com"
delete "changelog.txt"
delete "CREDITS.txt"
delete "LICENSE.txt"
delete "README.txt"
publish.dependsOn(clean)

task copyLicenses(type: Copy) {
    from 'build/reports/licenses'
    into 'lib_licences'
}
compileJava.dependsOn( copyLicenses )
copyLicenses.dependsOn( "licenseReport" )

version = "${major_version}.${minor_version}.${patch_version}"
group = "${domain}.${mod_id}" // http://maven.apache.org/guides/mini/guide-naming-conventions.html
archivesBaseName = "${mod_id}"

// Mojang ships Java 17 to end users in 1.18+, so your mod should target Java 17.
java.toolchain.languageVersion = JavaLanguageVersion.of(17)

println "Java: ${System.getProperty 'java.version'}, JVM: ${System.getProperty 'java.vm.version'} (${System.getProperty 'java.vendor'}), Arch: ${System.getProperty 'os.arch'}"
minecraft {
    // The mappings can be changed at any time and must be in the following format.
    // Channel:   Version:
    // official   MCVersion             Official field/method names from Mojang mapping files
    // parchment  YYYY.MM.DD-MCVersion  Open community-sourced parameter names and javadocs layered on top of official
    //
    // You must be aware of the Mojang license when using the 'official' or 'parchment' mappings.
    // See more information here: https://github.com/MinecraftForge/MCPConfig/blob/master/Mojang.md
    //
    // Parchment is an unofficial project maintained by ParchmentMC, separate from MinecraftForge
    // Additional setup is needed to use their mappings: https://github.com/ParchmentMC/Parchment/wiki/Getting-Started
    //
    // Use non-default mappings at your own risk. They may not always work.
    // Simply re-run your setup task after changing the mappings to update your workspace.
    mappings channel: "${mcp_channel}", version: "${mc_version}"

    // accessTransformer = file('src/main/resources/META-INF/accesstransformer.cfg') // Currently, this location cannot be changed from the default.

    // Default run configurations.
    // These can be tweaked, removed, or duplicated as needed.
    runs {
        client {
            workingDirectory project.file('run/client')

            // Recommended logging data for a userdev environment
            // The markers can be added/remove as needed separated by commas.
            // "SCAN": For mods scan.
            // "REGISTRIES": For firing of registry events.
            // "REGISTRYDUMP": For getting the contents of all registries.
            property 'forge.logging.markers', 'REGISTRIES'

            // Recommended logging level for the console
            // You can set various levels here.
            // Please read: https://stackoverflow.com/questions/2031163/when-to-use-the-different-log-levels
            property 'forge.logging.console.level', 'debug'

            // Comma-separated list of namespaces to load gametests from. Empty = all namespaces.
            property 'forge.enabledGameTestNamespaces', "${mod_id}"

            mods {
                "${mod_id}" {
                    source sourceSets.main
                }
            }
        }

        server {
            workingDirectory project.file('run/server')

            property 'forge.logging.markers', 'REGISTRIES'

            property 'forge.logging.console.level', 'debug'

            property 'forge.enabledGameTestNamespaces', "${mod_id}"

            mods {
                "${mod_id}" {
                    source sourceSets.main
                }
            }
        }

        // This run config launches GameTestServer and runs all registered gametests, then exits.
        // By default, the server will crash when no gametests are provided.
        // The gametest system is also enabled by default for other run configs under the /test command.
        gameTestServer {
            workingDirectory project.file('run/testserver')

            property 'forge.logging.markers', 'REGISTRIES'

            property 'forge.logging.console.level', 'debug'

            property 'forge.enabledGameTestNamespaces', "${mod_id}"

            mods {
                "${mod_id}" {
                    source sourceSets.main
                }
            }
        }

        data {
            workingDirectory project.file('run/data')

            property 'forge.logging.markers', 'REGISTRIES'

            property 'forge.logging.console.level', 'debug'

            // Specify the modid for data generation, where to output the resulting resource, and where to look for existing resources.
            args '--mod', "${mod_id}".toString(), '--all', '--output', file('src/generated/resources/'), '--existing', file('src/main/resources/')

            mods {
                "${mod_id}" {
                    source sourceSets.main
                }
            }
        }
    }
}

// Include resources generated by data generators.
sourceSets.main.resources { srcDir 'src/generated/resources' }

repositories {
    // Put repositories for dependencies here
    // ForgeGradle automatically adds the Forge maven and Maven Central for you

    // If you have mod jar dependencies in ./libs, you can declare them as a repository like so:
    // flatDir {
    //     dir 'libs'
    // }
    maven { url 'https://jitpack.io' }
    maven {
        name = "ModdingMavenRepositories"
        url = "file:../ModdingMavenRepositories"
    }
    mavenCentral() // for transitive dependencies
    maven {
        name 'm2-dv8tion'
        url 'https://m2.dv8tion.net/releases'
    }
}

dependencies {
    // Specify the version of Minecraft to use. If this is any group other than 'net.minecraft', it is assumed
    // that the dep is a ForgeGradle 'patcher' dependency, and its patches will be applied.
    // The userdev artifact is a special name and will get all sorts of transformations applied to it.
    minecraft "net.minecraftforge:forge:${mc_version}-${forge_version}"

    // Real mod deobf dependency examples - these get remapped to your current mappings
    // compileOnly fg.deobf("mezz.jei:jei-${mc_version}:${jei_version}:api") // Adds JEI API as a compile dependency
    // runtimeOnly fg.deobf("mezz.jei:jei-${mc_version}:${jei_version}") // Adds the full JEI mod as a runtime dependency
    // implementation fg.deobf("com.tterrag.registrate:Registrate:MC${mc_version}-${registrate_version}") // Adds registrate as a dependency

    compileOnly 'org.projectlombok:lombok:1.18.22'
    annotationProcessor 'org.projectlombok:lombok:1.18.22'


    minecraftLibrary "com.mantledillusion.essentials:json-patch-essentials:${json_patch_essentials_version}"
    minecraftLibrary "net.dv8tion:JDA:${jda_version}", {
        exclude module: 'opus-java'
        exclude module: 'slf4j-api'
    }
    minecraftLibrary "com.fasterxml.jackson.core:jackson-core:${jackson_version}"
    minecraftLibrary "com.fasterxml.jackson.core:jackson-annotations:${jackson_version}"
    minecraftLibrary "com.fasterxml.jackson.core:jackson-databind:${jackson_version}"
    minecraftLibrary "com.fasterxml.jackson.datatype:jackson-datatype-jsr310:${jackson_version}"

    shadow "com.mantledillusion.essentials:json-patch-essentials:${json_patch_essentials_version}"
    shadow "net.dv8tion:JDA:${jda_version}", {
        exclude module: 'opus-java'
        exclude module: 'slf4j-api'
    }
    shadow "com.fasterxml.jackson.core:jackson-core:${jackson_version}"
    shadow "com.fasterxml.jackson.core:jackson-annotations:${jackson_version}"
    shadow "com.fasterxml.jackson.core:jackson-databind:${jackson_version}"
    shadow "com.fasterxml.jackson.datatype:jackson-datatype-jsr310:${jackson_version}"

    runtimeOnly fg.deobf("de.geheimagentnr1.dimension_access_manager:DimensionAccessManager-${mc_version}:${dam_version}")
    runtimeOnly fg.deobf("de.geheimagentnr1.moremobgriefingoptions:MoreMobGriefingOptions-${mc_version}:${mmgo_version}")

    // Examples using mod jars from ./libs
    // implementation fg.deobf("blank:coolmod-${mc_version}:${coolmod_version}")

    // For more info...
    // http://www.gradle.org/docs/current/userguide/artifact_dependencies_tutorial.html
    // http://www.gradle.org/docs/current/userguide/dependency_management.html
}

configurations {
    implementation.extendsFrom(shadow)
}

shadowJar {
    classifier ""
    configurations = [project.configurations.shadow]

    relocate 'com.fasterxml.jackson.annotation', 'lib.com.fasterxml.jackson.annotation'
    relocate 'com.fasterxml.jackson.core', 'lib.com.fasterxml.jackson.core'
    relocate 'com.fasterxml.jackson.databind', 'lib.com.fasterxml.jackson.databind'
    relocate 'com.fasterxml.jackson.datatype.jsr310', 'lib.com.fasterxml.jackson.datatype.jsr310'
    relocate 'com.flipkart.zjsonpatch', 'lib.com.flipkart.zjsonpatch'
    relocate 'com.iwebpp.crypto', 'lib.com.iwebpp.crypto'
    relocate 'com.mantledillusion.essentials.json.patch', 'lib.com.mantledillusion.essentials.json.patch'
    relocate 'com.neovisionaries.ws.client', 'lib.com.neovisionaries.ws.client'
    relocate 'gnu.trove', 'lib.gnu.trove'
    relocate 'javax.annotation', 'lib.javax.annotation'
    relocate 'net.dv8tion.jda.annotations', 'lib.net.dv8tion.jda.annotations'
    relocate 'net.dv8tion.jda.api', 'lib.net.dv8tion.jda.api'
    relocate 'net.dv8tion.jda.internal', 'lib.net.dv8tion.jda.internal'
    relocate 'okhttp3', 'lib.okhttp3'
    relocate 'okio', 'lib.okio'
    relocate 'org.apache.commons.collections4', 'lib.org.apache.commons.collections4'
    relocate 'org.intellij.lang.annotations', 'lib.org.intellij.lang.annotations'
    relocate 'org.jetbrains.annotations', 'lib.org.jetbrains.annotations'
}

reobf {
    shadowJar {
        dependsOn createMcpToSrg
        mappings = createMcpToSrg.output
    }
}

artifacts {
    archives shadowJar
}

// Example for how to get properties into the manifest for reading at runtime.
jar {
    manifest {
        attributes([
                "Specification-Title"     : "${mod_id}",
                "Specification-Vendor"    : "${modder}",
                "Specification-Version"   : "1", // We are version 1 of ourselves
                "Implementation-Title"    : project.name,
                "Implementation-Version"  : "${version}",
                "Implementation-Vendor"   : "${modder}",
                "Implementation-Timestamp": new Date().format("yyyy-MM-dd'T'HH:mm:ssZ")
        ])
    }
}

// Example configuration to allow publishing using the maven-publish plugin
// This is the preferred method to reobfuscate your jar file
shadowJar.finalizedBy('reobfShadowJar')
// However if you are in a multi-project build, dev time needs unobfed jar files, so you can delay the obfuscation until publishing by doing
// publish.dependsOn('reobfJar')

publishing {
    publications {
        mavenJava(MavenPublication) {
            artifact shadowJar
        }
    }
    repositories {
        maven {
            url "file:../ModdingMavenRepositories"
        }
    }
}

tasks.withType(JavaCompile).configureEach {
    options.encoding = 'UTF-8' // Use the UTF-8 charset for Java compilation
}
