buildscript {
    repositories {
        jcenter()
        mavenCentral()
        maven { url = "http://files.minecraftforge.net/maven" }
    }
    dependencies {
        classpath group: 'net.minecraftforge.gradle', name: 'ForgeGradle', version: '3.+', changing: true
        classpath "com.github.jengelman.gradle.plugins:shadow:2.0.4"
    }
}

apply plugin: 'net.minecraftforge.gradle'
// Only edit below this line, the above code adds and enables the necessary things for Forge to be setup.
apply plugin: 'com.github.johnrengelman.shadow'
apply plugin: 'eclipse'
apply plugin: 'maven-publish'

repositories {
    jcenter()
}

version = "${major_version}.${minor_version}.${patch_version}"
group = 'de.geheimagentnr1.discordintegration' // http://maven.apache.org/guides/mini/guide-naming-conventions.html
archivesBaseName = 'discordintegration'

sourceCompatibility = targetCompatibility = compileJava.sourceCompatibility = compileJava.targetCompatibility = '1.8' // Need this here so eclipse task generates correctly.

minecraft {

    // The mappings can be changed at any time, and must be in the following format.
    // snapshot_YYYYMMDD   Snapshot are built nightly.
    // stable_#            Stables are built at the discretion of the MCP team.
    // Use non-default mappings at your own risk. they may not always work.
    // Simply re-run your setup task after changing the mappings to update your workspace.
    mappings channel: 'snapshot', version: "${mcp_version}-${mcp_mc_version}"
    // makeObfSourceJar = false // an Srg named sources jar is made by default. uncomment this to disable.
    // accessTransformer = file('build/resources/main/META-INF/accesstransformer.cfg')

    // Default run configurations.
    // These can be tweaked, removed, or duplicated as needed.
    runs {
        client {
            workingDirectory project.file('run')

            // Recommended logging data for a userdev environment
            property 'forge.logging.markers', 'SCAN,REGISTRIES,REGISTRYDUMP'

            // Recommended logging level for the console
            property 'forge.logging.console.level', 'debug'

            mods {
                discordintegration {
                    source sourceSets.main
                }
            }
        }

        server {
            workingDirectory project.file('run/server')

            // Recommended logging data for a userdev environment
            property 'forge.logging.markers', 'SCAN,REGISTRIES,REGISTRYDUMP'

            // Recommended logging level for the console
            property 'forge.logging.console.level', 'debug'

            mods {
                discordintegration {
                    source sourceSets.main
                }
            }
        }

        data {
            workingDirectory project.file('run')

            // Recommended logging data for a userdev environment
            property 'forge.logging.markers', 'SCAN,REGISTRIES,REGISTRYDUMP'

            // Recommended logging level for the console
            property 'forge.logging.console.level', 'debug'

            args '--mod', 'discordintegration', '--all', '--output', file('src/generated/resources/')

            mods {
                discordintegration {
                    source sourceSets.main
                }
            }
        }
    }
}


dependencies {
    // Specify the version of Minecraft to use, If this is any group other then 'net.minecraft' it is assumed
    // that the dep is a ForgeGradle 'patcher' dependency. And it's patches will be applied.
    // The userdev artifact is a special name and will get all sorts of transformations applied to it.
    minecraft "net.minecraftforge:forge:${mc_version}-${forge_version}"

    shadow("net.dv8tion:JDA:${jda_version}") {
        exclude module: 'opus-java'
    }
    shadow("org.slf4j:slf4j-simple:${slf4j_version}")
    shadow "club.minnced:discord-webhooks:${discord_webhooks_version}"
}

shadowJar {
    relocate 'org.apache.commons.collections4', 'lib.org.apache.commons.collections4'
    relocate 'gnu.trove', 'lib.gnu.trove'
    relocate "com.neovisionaries.ws.client", "lib.com.neovisionaries.ws.client"
    relocate "com.iwebpp.crypto", "lib.com.iwebpp.crypto"
    relocate "net.dv8tion.jda", "lib.net.dv8tion.jda"
    relocate "org.json", "lib.org.json"
    relocate "okio", "lib.okio"
    relocate "okhttp3", "lib.okhttp3"
    relocate "okhttp3", "lib.okhttp3"
    relocate "org.slf4j", "lib.org.slf4j"
    relocate "com.fasterxml.jackson", "lib.com.fasterxml.jackson"
    relocate "club.minnced.discord", "lib.club.minnced.discord"

    classifier ''
    configurations = [project.configurations.shadow]
    dependencies{
        exclude(dependency('org.jetbrains:annotations'))
        exclude(dependency('com.google.code.findbugs:jsr305'))
    }
}

reobf {
    shadowJar {
        dependsOn createMcpToSrg
        mappings = createMcpToSrg.output
    }
}

artifacts {
    archives shadowJar
}

// Example for how to get properties into the manifest for reading by the runtime..
jar {
    classifier "slim"
    manifest {
        attributes([
                "Specification-Title": "discordintegration",
                "Specification-Vendor": "GeheimagentNr1",
                "Specification-Version": "${version}", // We are version 1 of ourselves
                "Implementation-Title": project.name,
                "Implementation-Version": "${version}",
                "Implementation-Vendor" :"GeheimagentNr1",
                "Implementation-Timestamp": new Date().format("yyyy-MM-dd'T'HH:mm:ssZ")
        ])
    }
}

build.dependsOn(shadowJar)

def reobfFile = file("$buildDir/reobfShadowJar/output.jar")
def reobfArtifact = artifacts.add('default', reobfFile) {
    type 'jar'
    builtBy 'reobfShadowJar'
}
publishing {
    publications {
        mavenJava(MavenPublication) {
            artifact reobfArtifact
        }
    }
    repositories {
        maven {
            url "file:../ModdingMavenRepositories"
        }
    }
}